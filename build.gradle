
plugins {
    id 'java-gradle-plugin'

    id 'groovy'

    id 'com.gradle.plugin-publish' version '0.12.0'
}

group = 'com.github.andycandy-de'
version = '1.0.5'

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

gradlePlugin {
    plugins {
        userInput {
            id = 'de.andycandy.gradle.user_input'
            displayName = 'UserInputGradle'
            description = 'A simple plugin which adds user input dialogs to a grade build.'
            implementationClass = 'de.andycandy.gradle.user_input.UserInputGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    dependsOn(tasks.functionalTest)
}

pluginBundle {
    website = 'https://github.com/andycandy-de/UserInputGradle'
    vcsUrl = 'https://github.com/andycandy-de/UserInputGradle'
    tags = ['userinput', 'dialog', 'password']
}